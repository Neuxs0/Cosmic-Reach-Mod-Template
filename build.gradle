plugins {
    id 'java'
    id 'jigsaw-cosmic-reach'
}

group = mod_group
version = mod_version

jigsawGame {
    splitSourceSets()
}

jigsawInject {
    setModJson(file("src/common/resources/puzzle.mod.json"))
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url = 'https://maven.puzzleshq.dev/releases/' }
    maven { url = 'https://maven.puzzleshq.dev/snapshots/' }
}

dependencies {
    puzzleLoader "dev.puzzleshq:puzzle-loader-core:${puzzle_loader_core_version}"
    puzzleLoader "dev.puzzleshq:puzzle-loader-cosmic:${puzzle_loader_cosmic_version}"
    cosmicReach "finalforeach:cosmic-reach:${cosmic_reach_version}"
    commonImplementation "com.github.PuzzlesHQ:cosmic-api:${cosmic_api_version}"
}

def puzzleModExpandProps = [
    "mod_version": mod_version,
    "mod_id": mod_id,
    "mod_name": mod_name,
    "mod_description": mod_description,
    "mod_group": mod_group,
    "mod_license": mod_license,
    "mod_author": mod_author,

    // Keep version constraints centralized in gradle.properties.
    "cosmic_reach_dep": ">=${cosmic_reach_version}",
    "puzzle_loader_cosmic_dep": ">=${puzzle_loader_cosmic_version}"
]

tasks.withType(org.gradle.language.jvm.tasks.ProcessResources).configureEach {
    inputs.properties puzzleModExpandProps
    filesMatching("*.json") { expand puzzleModExpandProps }
}

java {
    toolchain { languageVersion = JavaLanguageVersion.of(24) }
}

tasks.named("clean", Delete).configure {
    delete(
            file("build"),
            file("runs")
    )
}

def cleanPostDirs = [file(".gradle"), file("build"), file("runs")]
def isCleanRequested = gradle.startParameter.taskNames.any { it == "clean" || it.endsWith(":clean") }

if (isCleanRequested) {
    gradle.buildFinished {
        rootProject.delete(cleanPostDirs)
    }
}

['runClient', 'rc', 'play'].each { alias ->
    tasks.register(alias) {
        dependsOn 'runModdedClient'
        group = 'verification'
        description = "Alias for runModdedClient"
    }
}
